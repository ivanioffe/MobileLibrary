plugins {
    `kotlin-dsl`
    alias(libs.plugins.ktlint)
}

group = "com.ioffeivan.mobilelibrary.buildlogic"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

dependencies {
    compileOnly(libs.android.gradleApiPlugin)
    compileOnly(libs.kotlin.gradlePlugin)
    compileOnly(libs.roborazzi.gradlePlugin)
}

tasks {
    validatePlugins {
        enableStricterValidation = true
        failOnWarning = true
    }
}

gradlePlugin {
    plugins {
        register("androidApplication") {
            id = libs.plugins.ioffeivan.android.application.get().pluginId
            implementationClass = "AndroidApplicationConventionPlugin"
        }
        register("androidLibrary") {
            id = libs.plugins.ioffeivan.android.library.get().pluginId
            implementationClass = "AndroidLibraryConventionPlugin"
        }
        register("compose") {
            id = libs.plugins.ioffeivan.compose.get().pluginId
            implementationClass = "ComposeConventionPlugin"
        }
        register("datastore") {
            id = libs.plugins.ioffeivan.datastore.get().pluginId
            implementationClass = "DataStoreConventionPlugin"
        }
        register("hilt") {
            id = libs.plugins.ioffeivan.hilt.get().pluginId
            implementationClass = "HiltConventionPlugin"
        }
        register("jvmLibrary") {
            id = libs.plugins.ioffeivan.jvm.library.get().pluginId
            implementationClass = "JvmLibraryConventionPlugin"
        }
        register("screenshotTesting") {
            id = libs.plugins.ioffeivan.screenshotTesting.get().pluginId
            implementationClass = "ScreenshotTestingConventionPlugin"
        }
    }
}
